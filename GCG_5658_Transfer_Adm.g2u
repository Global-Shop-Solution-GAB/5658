Program.Sub.ScreenSU.Start
Gui.Form..Create
Gui.Form..Size(6630,10470)
Gui.Form..MinX(0)
Gui.Form..MinY(0)
Gui.Form..Position(0,0)
Gui.Form..BackColor(-2147483633)
Gui.Form..MousePointer(0)
Gui.Form..Event(UnLoad,Form_UnLoad)
Gui.Form..Caption("Buyer Code Maintenance")
Gui.Form.frame1.Create(Frame)
Gui.Form.frame1.Size(6195,1290)
Gui.Form.frame1.Position(90,60)
Gui.Form.lbl1.Create(Label,"Buyer Code",True,1050,255,0,60,195,True,0,"Arial",8,-2147483633,0)
Gui.Form.lbl1.Parent("frame1")
Gui.Form.txtBuyerCode.Create(TextBox,"",True,1215,300,0,60,420,True,0,"Arial",8,-2147483643,1)
Gui.Form.txtBuyerCode.Parent("frame1")
Gui.Form.lbl2.Create(Label,"Customer",True,1050,255,0,1410,195,True,0,"Arial",8,-2147483633,0)
Gui.Form.lbl2.Parent("frame1")
Gui.Form.lbl3.Create(Label,"Name",True,1050,255,0,3270,195,True,0,"Arial",8,-2147483633,0)
Gui.Form.lbl3.Parent("frame1")
Gui.Form.txtCustomer.Create(TextBox,"",True,1215,300,0,1395,420,True,0,"Arial",8,-2147483643,1)
Gui.Form.txtCustomer.Parent("frame1")
Gui.Form.cmdCustomerBrw.Create(Button)
Gui.Form.cmdCustomerBrw.Size(375,375)
Gui.Form.cmdCustomerBrw.Position(2670,375)
Gui.Form.cmdCustomerBrw.Parent("frame1")
Gui.Form.cmdCustomerBrw.Caption("^")
Gui.Form.cmdCustomerBrw.Event(Click,cmdCustomerBrw_Click)
Gui.Form.txtName.Create(TextBox,"",True,2850,300,0,3225,420,True,0,"Arial",8,-2147483643,1)
Gui.Form.txtName.Parent("frame1")
Gui.Form.cmdAdd.Create(Button)
Gui.Form.cmdAdd.Size(855,375)
Gui.Form.cmdAdd.Position(5205,780)
Gui.Form.cmdAdd.Parent("frame1")
Gui.Form.cmdAdd.Caption("Add")
Gui.Form.cmdAdd.Event(Click,cmdAdd_Click)
Gui.Form.cmdDelete.Create(Button)
Gui.Form.cmdDelete.Size(855,375)
Gui.Form.cmdDelete.Position(4245,780)
Gui.Form.cmdDelete.Parent("frame1")
Gui.Form.cmdDelete.Caption("Delete")
Gui.Form.cmdDelete.Event(Click,cmdDelete_Click)
Gui.Form.lbl4.Create(Label,"From Loc:",True,1185,255,0,90,885,True,1,"Arial",8,-2147483633,0)
Gui.Form.lbl4.Parent("frame1")
Gui.Form.txtFromLoc.Create(TextBox,"",True,1215,300,0,1395,810,True,0,"Arial",8,-2147483643,1)
Gui.Form.txtFromLoc.Parent("frame1")
Gui.Form.frame2.Create(Frame)
Gui.Form.frame2.Size(6195,8460)
Gui.Form.frame2.Position(90,1350)
Gui.Form.GsGcDisplay.Create(GsGridControl)
Gui.Form.GsGcDisplay.Size(6075,8175)
Gui.Form.GsGcDisplay.Position(60,150)
Gui.Form.GsGcDisplay.Parent("frame2")
Gui.Form.GsGcDisplay.Event(RowCellClick,GsGcDisplay_RowCellClick)
Program.Sub.ScreenSU.End

Program.Sub.Preflight.Start
Program.Sub.Preflight.End

Program.Sub.Main.Start
f.Intrinsic.Control.Try
	f.Intrinsic.Control.CallSub(createDisplayDT)
	f.Intrinsic.Control.CallSub(loadGridProperties)
	f.Intrinsic.Control.CallSub(LoadIcon)
	gui.Form..Show
f.Intrinsic.Control.Catch
	f.Intrinsic.Control.CallSub(ErrorMsg, CurrentSub, v.Ambient.CurrentSubroutine)
f.Intrinsic.Control.EndTry
Program.Sub.Main.End

Program.Sub.Form_UnLoad.Start
f.Intrinsic.Control.Try
	f.Intrinsic.Control.End
f.Intrinsic.Control.Catch
	f.Intrinsic.Control.CallSub(ErrorMsg, CurrentSub, v.Ambient.CurrentSubroutine)
f.Intrinsic.Control.EndTry
Program.Sub.Form_UnLoad.End

Program.Sub.cmdCustomerBrw_Click.Start
v.Local.sRet.Declare
f.Intrinsic.Control.Try
	f.Intrinsic.UI.Browser(205, , v.Local.sRet)
	f.Intrinsic.Control.If(v.Local.sRet, <>, "***CANCEL***")
		f.Intrinsic.String.Split(v.Local.sRet, "*!*", v.Local.sRet)
		gui.Form.txtCustomer.Text(v.Local.sRet(0))
		gui.Form.txtName.Text(v.Local.sRet(1))
	f.Intrinsic.Control.EndIf
f.Intrinsic.Control.Catch
	f.Intrinsic.Control.CallSub(ErrorMsg, CurrentSub, v.Ambient.CurrentSubroutine)
f.Intrinsic.Control.EndTry
Program.Sub.cmdCustomerBrw_Click.End

Program.Sub.cmdAdd_Click.Start
v.Local.sBuyerCode.Declare
v.Local.sCustomer.Declare
v.Local.sName.Declare
v.Local.sFromLoc.Declare
v.Local.sSQL.Declare
v.Local.sRet.Declare
f.Intrinsic.Control.Try
	v.Local.sBuyerCode.Set(v.Screen.Form!txtBuyerCode.Text)
	v.Local.sCustomer.Set(v.Screen.Form!txtCustomer.Text)
	v.Local.sName.Set(v.Screen.Form!txtName.Text)
	v.Local.sFromLoc.Set(v.Screen.Form!txtFromLoc.Text)
	
	f.Intrinsic.Control.If(v.Local.sBuyerCode.Trim, =, "", OR, v.Local.sCustomer.Trim, =, "", v.Local.sName.Trim, =, "", v.Local.sFromLoc.Trim, =, "")
		f.Intrinsic.UI.Msgbox("Please fill out all text fields.")
		f.Intrinsic.Control.ExitSub
	f.Intrinsic.Control.EndIf
	
	f.ODBC.Connection!con.OpenCompanyConnection
	 	f.Intrinsic.String.Build("Select * from GCG_5658_BuyerCodes where Customer = '{0}'", v.Local.sCustomer.Trim, v.Local.sSQL)
	 	f.ODBC.Connection!con.ExecuteAndReturn(v.Local.sSQL, v.Local.sRet)
	f.ODBC.Connection!con.Close
	
	f.Intrinsic.Control.If(v.Local.sRet.Trim, <>, "")
		f.Intrinsic.UI.Msgbox("Customer Already exists. Please select a new one.")
		f.Intrinsic.Control.ExitSub
	f.Intrinsic.Control.EndIf
	
	f.ODBC.Connection!con.OpenCompanyConnection
	 	f.Intrinsic.String.Build("Insert into  GCG_5658_BuyerCodes (BuyerCode, FromLoc, Customer, Name) Values ('{0}', '{1}', '{2}', '{3}')", v.Local.sBuyerCode, v.Local.sFromLoc, v.Local.sCustomer, v.Local.sName, v.Local.sSQL)
	 	f.ODBC.Connection!con.Execute(v.Local.sSQL)
	f.ODBC.Connection!con.Close
	
	f.ODBC.Connection!con.OpenCompanyConnection
		f.Data.DataTable.CreateFromSQL("tmpDisplayDT", "con", "Select * from GCG_5658_BuyerCodes", true)
	f.ODBC.Connection!con.Close
	
	f.Data.DataTable.DeleteRow("displayDT")
	f.Data.DataTable.Merge("tmpDisplayDT", "displayDT", false, 2)
	f.Data.DataTable.Close("tmpDisplayDT")
f.Intrinsic.Control.Catch
	f.Intrinsic.Control.CallSub(ErrorMsg, CurrentSub, v.Ambient.CurrentSubroutine)
f.Intrinsic.Control.EndTry
Program.Sub.cmdAdd_Click.End

Program.Sub.cmdDelete_Click.Start
v.Local.sBuyerCode.Declare
v.Local.sCustomer.Declare
v.Local.sName.Declare
v.Local.sSQL.Declare
v.Local.sFromLoc.Declare
v.Local.sRet.Declare
f.Intrinsic.Control.Try
	v.Local.sBuyerCode.Set(v.Screen.Form!txtBuyerCode.Text)
	v.Local.sCustomer.Set(v.Screen.Form!txtCustomer.Text)
	v.Local.sName.Set(v.Screen.Form!txtName.Text)
	v.Local.sFromLoc.Set(v.Screen.Form!txtFromLoc.Text)
	f.Intrinsic.Control.If(v.Local.sBuyerCode.Trim, =, "", OR, v.Local.sCustomer.Trim, =, "", v.Local.sName.Trim, =, "", v.Local.sFromLoc.Trim, =, "")
		f.Intrinsic.UI.Msgbox("Please select a buyer code.")
		f.Intrinsic.Control.ExitSub
	f.Intrinsic.Control.EndIf
	
	f.ODBC.Connection!con.OpenCompanyConnection
	 	f.Intrinsic.String.Build("Select * from GCG_5658_BuyerCodes where Customer = '{0}'", v.Local.sCustomer.Trim, v.Local.sSQL)
	 	f.ODBC.Connection!con.ExecuteAndReturn(v.Local.sSQL, v.Local.sRet)
	f.ODBC.Connection!con.Close
	
	f.Intrinsic.Control.If(v.Local.sRet.Trim, =, "")
		f.Intrinsic.UI.Msgbox("Customer doesn't exists. Please select a new one.")
		f.Intrinsic.Control.ExitSub
	f.Intrinsic.Control.EndIf

	f.ODBC.Connection!con.OpenCompanyConnection
		f.Intrinsic.String.Build("Delete from GCG_5658_BuyerCodes where BuyerCode = '{0}' and Customer = '{1}'", v.Local.sBuyerCode, v.Local.sCustomer, v.Local.sSQL)
		f.ODBC.Connection!con.Execute(v.Local.sSQL)
	f.ODBC.Connection!con.Close
	
	f.ODBC.Connection!con.OpenCompanyConnection
		f.Data.DataTable.CreateFromSQL("tmpDisplayDT", "con", "Select * from GCG_5658_BuyerCodes", true)
	f.ODBC.Connection!con.Close
	
	f.Data.DataTable.DeleteRow("displayDT")
	f.Data.DataTable.AcceptChanges("displayDT")
	f.Data.DataTable.Merge("tmpDisplayDT", "displayDT", false, 2)
	f.Data.DataTable.Close("tmpDisplayDT")
f.Intrinsic.Control.Catch
	f.Intrinsic.Control.CallSub(ErrorMsg, CurrentSub, v.Ambient.CurrentSubroutine)
f.Intrinsic.Control.EndTry
Program.Sub.cmdDelete_Click.End

Program.Sub.GsGcDisplay_RowCellClick.Start
v.Local.sBuyerCode.Declare
v.Local.sCustomer.Declare
v.Local.sName.Declare
v.Local.sFromLoc.Declare
f.Intrinsic.Control.Try
	v.Local.sBuyerCode.Set(v.DataTable.displayDT(v.Args.RowIndex).BuyerCode!FieldValTrim)
	v.Local.sCustomer.Set(v.DataTable.displayDT(v.Args.RowIndex).Customer!FieldValTrim)
	v.Local.sName.Set(v.DataTable.displayDT(v.Args.RowIndex).Name!FieldValTrim)
	v.Local.sFromLoc.Set(v.DataTable.displayDT(v.Args.RowIndex).FromLoc!FieldValTrim)
	
	gui.Form.txtBuyerCode.Text(v.Local.sBuyerCode)
	gui.Form.txtCustomer.Text(v.Local.sCustomer)
	gui.Form.txtName.Text(v.Local.sName)
	gui.Form.txtFromLoc.Text(v.Local.sFromLoc)
f.Intrinsic.Control.Catch
	f.Intrinsic.Control.CallSub(ErrorMsg, CurrentSub, v.Ambient.CurrentSubroutine)
f.Intrinsic.Control.EndTry
Program.Sub.GsGcDisplay_RowCellClick.End

Program.Sub.ErrorMsg.Start
v.Local.sError.Declare

'Closes ODBC connection, default to connection : "con"
f.Intrinsic.Control.If(v.ODBC!con.State, =, 1)
	f.ODBC.Connection!con.Close
f.Intrinsic.Control.EndIf

'Generic Error message.
f.Intrinsic.String.Build("Project GCG_5658_Transfer_Adm.g2u {0}{0}Subroutine: {1}{0}Error: {2} with Description: {3}", v.Ambient.NewLine, v.Args.CurrentSub, v.Ambient.ErrorNumber, v.Ambient.ErrorDescription, v.Local.sError)
f.Intrinsic.UI.Msgbox(v.Local.sError)
Program.Sub.ErrorMsg.End

Program.Sub.createDisplayDT.Start
f.Intrinsic.Control.Try
	f.Data.DataTable.Create("displayDT", true)
	f.Data.DataTable.AddColumn("displayDT", "BuyerCode", "String")
	f.Data.DataTable.AddColumn("displayDT", "FromLoc", "String")
	f.Data.DataTable.AddColumn("displayDT", "Customer", "String")
	f.Data.DataTable.AddColumn("displayDT", "Name", "String")

	f.ODBC.Connection!con.OpenCompanyConnection
		f.Data.DataTable.CreateFromSQL("tmpDisplayDT", "con", "Select * from GCG_5658_BuyerCodes", true)
	f.ODBC.Connection!con.Close
	
	f.Data.DataTable.DeleteRow("displayDT")
	f.Data.DataTable.Merge("tmpDisplayDT", "displayDT", false, 2)
	f.Data.DataTable.Close("tmpDisplayDT")
	
	f.Data.DataView.Create("displayDT", "displayDV", 22)
	gui.Form.GsGcDisplay.AddGridviewFromDataview("displayGV", "displayDT", "displayDV")
	gui.Form.GsGcDisplay.MainView("displayGV")
f.Intrinsic.Control.Catch
	f.Intrinsic.Control.CallSub(ErrorMsg, CurrentSub, v.Ambient.CurrentSubroutine)
f.Intrinsic.Control.EndTry
Program.Sub.createDisplayDT.End

Program.Sub.loadGridProperties.Start
v.Local.iCnt.Declare
v.Local.sFields.Declare
f.Intrinsic.Control.Try
	f.Intrinsic.String.Split(v.DataTable.displayDT.FieldNames, "*!*", v.Local.sFields)
	f.Intrinsic.Control.For(v.Local.iCnt, 0, v.Local.sFields.UBound, 1)
		gui.Form.GsGcDisplay.SetColumnProperty("displayGV", v.Local.sFields(v.Local.iCnt), "HeaderFontBold", True)
		gui.Form.GsGcDisplay.SetColumnProperty("displayGV", v.Local.sFields(v.Local.iCnt), "Visible", True)
		gui.Form.GsGcDisplay.SetColumnProperty("displayGV", v.Local.sFields(v.Local.iCnt), "ReadOnly", True)
		gui.Form.GsGcDisplay.SetColumnProperty("displayGV", v.Local.sFields(v.Local.iCnt), "AllowEdit", False)
		gui.Form.GsGcDisplay.SetColumnProperty("displayGV", v.Local.sFields(v.Local.iCnt), "HeaderHAlignment", "Near")
		gui.Form.GsGcDisplay.SetColumnProperty("displayGV", v.Local.sFields(v.Local.iCnt), "CellHAlignment", "Near")
		gui.Form.GsGcDisplay.SetColumnProperty("displayGV", v.Local.sFields(v.Local.iCnt), "HeaderBackColor", v.Color.GSS)
		gui.Form.GsGcDisplay.SetColumnProperty("displayGV", v.Local.sFields(v.Local.iCnt), "HeaderForeColor", v.Color.White)
	f.Intrinsic.Control.Next(v.Local.iCnt)
	
	gui.Form.GsGcDisplay.BestFitColumns("displayGV")
'	gui.Form.GsGcDisplay.SetColumnProperty("displayGV", "Default", "ReadOnly", False)
'	gui.Form.GsGcDisplay.SetColumnProperty("displayGV", "Default", "AllowEdit", True)
'	gui.Form.GsGcDisplay.SetColumnProperty("displayGV", "Default", "HeaderHAlignment", "Center")
'	gui.Form.GsGcDisplay.SetColumnProperty("displayGV", "Default", "CellHAlignment", "Center")
f.Intrinsic.Control.Catch
	f.Intrinsic.Control.CallSub(ErrorMsg, CurrentSub, v.Ambient.CurrentSubroutine)
f.Intrinsic.Control.EndTry
Program.Sub.loadGridProperties.End

Program.Sub.loadIcon.Start
v.Local.sIcon.Declare
'sets the icon
f.Intrinsic.String.Build("{0}\ART\gss2.ico", v.Caller.GlobalDir, v.Local.sIcon)
gui.Form..Icon(v.Local.sIcon)
Program.Sub.loadIcon.End
